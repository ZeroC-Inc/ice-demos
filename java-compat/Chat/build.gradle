buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.guardsquare:proguard-gradle:7.4.0-beta02"
    }
}

slice {
    java {
        include = ["${projectDir}"]
        srcDir = '.'
        include = ["${projectDir}"]
    }
}

dependencies {
    implementation 'com.jgoodies:jgoodies-common:1.8.0'
    implementation 'com.jgoodies:jgoodies-looks:2.6.0'
    implementation 'com.jgoodies:jgoodies-forms:1.8.0'
    implementation localDependency("ice-compat")
    implementation localDependency("glacier2-compat")
}

jar {
    archiveFileName = "ChatGUITEMP.jar"
    manifest {
        attributes 'Main-Class': "ChatDemoGUI.MainView"
    }
}

def javaHome = System.properties['java.home']

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    injars configurations.runtimeClasspath.resolve(), filter: '!META-INF/**'
    injars "${buildDir}/libs/ChatGUITEMP.jar"
    if(JavaVersion.current() <= JavaVersion.VERSION_1_8) {
        libraryjars "${javaHome}/lib/rt.jar"
        libraryjars "${javaHome}/lib/jsse.jar"
    } else {
        libraryjars "${javaHome}/jmods/java.base.jmod"
        libraryjars "${javaHome}/jmods/java.logging.jmod"
        libraryjars "${javaHome}/jmods/java.desktop.jmod"
        libraryjars "${javaHome}/jmods/java.prefs.jmod"
    }
    outjars "ChatGUI.jar"
    configuration "chatdemogui.pro"
}
assemble.dependsOn(proguard)


clean {
    delete("ChatGUI.jar")
}
